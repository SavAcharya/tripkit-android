apply plugin: "com.android.library"
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jfrog.bintray'

// For https://github.com/dcendents/android-maven-gradle-plugin.
group = 'com.skedgo'
version = '1.1'

android {
  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    minSdkVersion 15
    targetSdkVersion 25
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    // To avoid conflicts with ASL.
    // We don't utilize ServiceLoader, so this is unneeded.
    exclude 'META-INF/services/com.google.gson.TypeAdapterFactory'
  }

  lintOptions {
    checkReleaseBuilds true
    abortOnError false
  }
}

// Generation relative to tripgo! Add translations repo to tripkit?
task(generateTripkitStrings, type: org.gradle.api.tasks.JavaExec) {
  classpath(files('IOStoAndroidStringsConverter.jar'))
  main('com.skedgo.tripgo.tools.android.Main')
  args(['./src/main/res', '../../libraries/translations', 'void',
        'en#es#de#fi#pt#zh-Hant#zh-Hans', 'Tripkit.strings'])
}

dependencies {
  testImplementation libs.junit
  testImplementation libs.assertjCore
  testImplementation libs.robolectric
  testImplementation libs.mockitoCore
  testImplementation libs.mockitoKotlin, { exclude group: 'org.jetbrains.kotlin' }
  testImplementation libs.kluent
  testImplementation 'com.github.thuytrinh:MockWebServerRule:v1.0'
  testImplementation libs.kluent

  androidTestImplementation "com.android.support.test:runner:0.5"
  androidTestImplementation "com.android.support.test:rules:0.5"
  androidTestImplementation libs.assertjCore

  implementation libs.kotlin
  implementation libs.playServicesGcm
  implementation libs.jodaTimeAndroid
  implementation 'com.github.skedgo:commons-collections:v1.0'

  implementation 'com.github.skedgo:sqlite-utils:v1.0'
  implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
  implementation "com.squareup.retrofit2:converter-gson:2.0.2"
  implementation "com.squareup.retrofit2:adapter-rxjava:2.0.2"
  implementation "com.squareup.retrofit2:retrofit:2.0.2"
  implementation libs.playServicesMaps
  implementation libs.rxjava
  implementation 'io.reactivex:rxandroid:1.2.1', { exclude module: 'rxjava' }
  implementation 'com.github.skedgo:android-maps-utils:241fc89'

  implementation libs.dagger
  kapt libs.daggerCompiler
  kapt libs.value
  compileOnly libs.valueAnnotations
  compileOnly libs.builderAnnotations
  compileOnly libs.gsonAnnotations
  implementation project(':CommonCoreLegacy')
  implementation project(':TripKitDomain')
  implementation project(':TripKitData')
}

install {
  repositories.mavenInstaller {
    pom.project {
      name 'TripKitAndroid'
      description 'TripKit for Android'
      url 'https://github.com/skedgo/tripkit-android.git'
      inceptionYear '2013'

      packaging 'aar'
      groupId project.group
      artifactId 'TripKitAndroid'
      version project.version

      licenses {
        license {
          name 'The MIT License'
          url 'https://spdx.org/licenses/MIT#licenseText'
          distribution 'repo'
        }
      }
      developers {
        developer {
          name 'SkedGo'
        }
      }
    }
  }
}

bintray {
  user = BINTRAY_USER
  key = BINTRAY_KEY
  configurations = ['archives']
  pkg {
    repo = 'tripkit-android'
    name = 'TripKitAndroid'
    userOrg = 'skedgo'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/skedgo/tripkit-android.git'
    version {
      name = project.version
      desc = "TripKitAndroid $project.version"
      released = new Date()
      vcsTag = project.version
    }
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      repository(url: "file://${tripKitAndroidMaven}")
    }
  }
}
